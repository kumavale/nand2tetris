// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static Array screen;    // Screen pointer
    static Array pow_of_2;  // Powers of 2
    static boolean color;   // if true then black else white

    /** Initializes the Screen. */
    function void init() {
        var int i, val;
        let screen = 16384;
        let color = true;

        let i = 0;
        let val = 1;
        let pow_of_2 = Array.new(16);
        while (i < 16) {
            let pow_of_2[i] = val;
            let val = val + val;
            let i = i + 1;
        }

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        while (i < 8192) {  // 512*256/16
            let screen[i] = 0;
            let i = i + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;

        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, bit, mask;
        let address = (y*32) + (x/16);
        let bit = x & 15;  // bit = x % 16
        let mask = pow_of_2[bit];

        if (color) {
            let screen[address] = screen[address] | mask;   // Black
        } else {
            let screen[address] = screen[address] & ~mask;  // White
        }

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    /** Bresenham's line algorithm */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var boolean steep;
        var int x, y, dx, dy, err, ystep, inc;
        let steep = Math.abs(y2 - y1) > Math.abs(x2 - x1);

        if (steep) {
            var int tmp;
            // swap(x1, y1);
            let tmp = x1;
            let x1 = y1;
            let y1 = tmp;
            // swap(x2, y2);
            let tmp = x2;
            let x2 = y2;
            let y2 = tmp;
        }

        let dx = Math.abs(x2 - x1);
        let dy = Math.abs(y2 - y1);
        let err = dx / 2;
        let y = y1;

        if (x1 < x2) {
            let inc = 1;
        } else {
            let inc = -1;
        }

        if (y1 < y2) {
            let ystep = 1;
        } else {
            let ystep = -1;
        }

        let x = x1;
        while (~(x = x2)) {
            if (steep) {
                do Screen.drawPixel(y, x);
            } else {
                do Screen.drawPixel(x, y);
            }
            let err = err - dy;
            if (err < 0) {
                let y = y + ystep;
                let err = err + dx;
            }
            let x = x + inc;
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int dx, dy;
        let dx = x2 + 1;
        let dy = y1;

        while (dy < y2) {
            do Screen.drawLine(x1, dy, dx, dy);
            let dy = dy + 1;
        }

        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dx, dy, r_squared;
        let dy = -r;
        let r_squared = r * r;

        while (~(dy > r)) {
            let dx = Math.sqrt(r_squared - (dy * dy));
            do Screen.drawLine(x-dx, y+dy, x+dx, y+dy);
            let dy = dy + 1;
        }

        return;
    }
}

